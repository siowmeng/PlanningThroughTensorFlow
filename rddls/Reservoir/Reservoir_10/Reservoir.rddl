//////////////////////////////////////
//Reservior.rddl
//
//Auther: Buser Say, Ga Wu
//////////////////////////////////////

domain Reservoir_Problem{

	requirements = { 
		reward-deterministic 
	};

	types {
		id: object;
	};
	
	pvariables {
	
		// Constant
		MAXCAP(id): { non-fluent, real, default = 300.0 };
		HIGH_BOUND(id): { non-fluent, real, default = 280.0 };
		LOW_BOUND(id): { non-fluent, real, default = 20.0 };
		RAIN(id): { non-fluent ,real, default = 25.0 };
		DOWNSTREAM(id,id): {non-fluent ,bool, default = false };
		DOWNTOSEA(id): {non-fluent, bool, default = false };
		BIGGESTMAXCAP: {non-fluent, real, default = 1000};

		//Interm
		vaporated(id): {interm-fluent, real};
		
		//State
		rlevel(id): {state-fluent, real, default = 150.0 };
		
		//Action
		flow(id): { action-fluent, real, default = 0.0 };
	};
	
	cpfs {
		vaporated(?r) = (1.0/2.0)*sin[rlevel(?r)/BIGGESTMAXCAP]*rlevel(?r);
		rlevel'(?r) = rlevel(?r) + RAIN(?r)- vaporated(?r) - flow(?r) + sum_{?r2: id}[DOWNSTREAM(?r2,?r)*flow(?r2)];

	};
	
	reward = sum_{?r: id} [if (rlevel'(?r)>=LOW_BOUND(?r) ^ (rlevel'(?r)<=HIGH_BOUND(?r)))
									then 0
 									else if (rlevel'(?r)<=LOW_BOUND(?r))
 										then (-5)*(LOW_BOUND(?r)-rlevel'(?r))
										else (-100)*(rlevel'(?r)-HIGH_BOUND(?r))]+sum_{?r2:id}[abs[((HIGH_BOUND(?r2)+LOW_BOUND(?r2))/2.0)-rlevel'(?r2)]*(-0.1)];
								
	state-action-constraints {
	
		forall_{?r:id} flow(?r)<=rlevel(?r);
		forall_{?r:id} rlevel(?r)<=MAXCAP(?r);
		forall_{?r:id} flow(?r)>=0;
	};

}

non-fluents Reservoir_non {
	domain = Reservoir_Problem;
	objects{
		id: {t1,t2,t3,t4,t5,t6,t7,t8,t9,t10};
	};
	non-fluents {
		RAIN(t1) = 15.0;
		RAIN(t2) = 10.0;
		RAIN(t3) = 20.0;
		RAIN(t4) = 30.0;
		RAIN(t5) = 20.0;
		RAIN(t6) = 10.0;
		RAIN(t7) = 35.0;
		RAIN(t8) = 15.0;
		RAIN(t9) = 25.0;
		RAIN(t10) = 20.0;
		MAXCAP(t2) = 200.0;
		LOW_BOUND(t2) = 30.0;
		HIGH_BOUND(t2) = 180.0;
		MAXCAP(t3) = 400.0;
		LOW_BOUND(t3) = 40.0;
		HIGH_BOUND(t3) = 380.0;
		MAXCAP(t4) = 500.0;
		LOW_BOUND(t4) = 60.0;
		HIGH_BOUND(t4) = 480.0;
		MAXCAP(t5) = 750.0;
		LOW_BOUND(t5) = 20.0;
		HIGH_BOUND(t5) = 630.0;
		MAXCAP(t6) = 300.0;
		LOW_BOUND(t6) = 30.0;
		HIGH_BOUND(t6) = 250.0;
		MAXCAP(t7) = 300.0;
		LOW_BOUND(t7) = 10.0;
		HIGH_BOUND(t7) = 180.0;
		MAXCAP(t8) = 300.0;
		LOW_BOUND(t8) = 40.0;
		HIGH_BOUND(t8) = 240.0;
		MAXCAP(t9) = 400.0;
		LOW_BOUND(t9) = 40.0;
		HIGH_BOUND(t9) = 340.0;
		MAXCAP(t10) = 800.0;
		LOW_BOUND(t10) = 20.0;
		HIGH_BOUND(t10) = 650.0;
		DOWNSTREAM(t1,t2);DOWNSTREAM(t2,t3);DOWNSTREAM(t3,t4);DOWNSTREAM(t4,t5);
		DOWNSTREAM(t6,t7);DOWNSTREAM(t7,t8);DOWNSTREAM(t8,t5);
		DOWNSTREAM(t5,t6);DOWNSTREAM(t6,t10);
		DOWNSTREAM(t5,t9);DOWNSTREAM(t9,t10);
		DOWNTOSEA(t10);
	};
}

instance is1{
	domain = Reservoir_Problem;
	non-fluents = Reservoir_non;
	init-state{
		rlevel(t1) = 175.0;
	};
	max-nondef-actions = 10;
	horizon = 10;
	discount = 1.0;
}
