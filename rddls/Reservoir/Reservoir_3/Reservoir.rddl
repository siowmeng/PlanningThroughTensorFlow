//////////////////////////////////////
//Reservior.rddl
//
//Auther:GA WU
//////////////////////////////////////

domain Reservoir_Problem{

	requirements = { 
		reward-deterministic 
	};

	types {
		id: object;
	};
	
	pvariables {
	
		// Constant
		MAXCAP(id): { non-fluent, real, default = 100.0 };
		HIGH_BOUND(id): { non-fluent, real, default = 80.0 };
		LOW_BOUND(id): { non-fluent, real, default = 20.0 };
		RAIN(id): { non-fluent ,real, default = 5.0 };
		DOWNSTREAM(id,id): {non-fluent ,bool, default = false };
		DOWNTOSEA(id): {non-fluent, bool, default = false };
		BIGGESTMAXCAP: {non-fluent, real, default = 1000};

		//Interm
		vaporated(id): {interm-fluent, real};
		
		//State
		rlevel(id): {state-fluent, real, default = 50.0 };
		
		//Action
		flow(id): { action-fluent, real, default = 0.0 };
	};
	
	cpfs {
		vaporated(?r) = (1.0/2.0)*sin[rlevel(?r)/BIGGESTMAXCAP]*rlevel(?r);
		rlevel'(?r) = rlevel(?r) + RAIN(?r)- vaporated(?r) - flow(?r) + sum_{?r2: id}[DOWNSTREAM(?r2,?r)*flow(?r2)];

	};
	
	reward = sum_{?r: id} [if (rlevel'(?r)>=LOW_BOUND(?r) ^ (rlevel'(?r)<=HIGH_BOUND(?r)))
									then 0
 									else if (rlevel'(?r)<=LOW_BOUND(?r))
 										then (-5)*(LOW_BOUND(?r)-rlevel'(?r))
										else (-100)*(rlevel'(?r)-HIGH_BOUND(?r))]+sum_{?r2:id}[abs[((HIGH_BOUND(?r2)+LOW_BOUND(?r2))/2.0)-rlevel'(?r2)]*(-0.1)];
								
	state-action-constraints {
	
		forall_{?r:id} flow(?r)<=rlevel(?r);
		forall_{?r:id} rlevel(?r)<=MAXCAP(?r);
		forall_{?r:id} flow(?r)>=0;
	};

}

non-fluents Reservoir_non {
	domain = Reservoir_Problem;
	objects{
		id: {t1,t2,t3};
	};
	non-fluents {
		RAIN(t1) = 5.0;
		RAIN(t2) = 10.0;
		RAIN(t3) = 20.0;
		MAXCAP(t2) = 200.0;
		LOW_BOUND(t2) = 30.0;
		HIGH_BOUND(t2) = 180.0;
		MAXCAP(t3) = 400.0;
		LOW_BOUND(t3) = 40.0;
		HIGH_BOUND(t3) = 380.0;
		DOWNSTREAM(t1,t2);DOWNSTREAM(t2,t3);DOWNTOSEA(t3);
	};
}

instance is1{
	domain = Reservoir_Problem;
	non-fluents = Reservoir_non;
	init-state{
		rlevel(t1) = 75.0;
	};
	max-nondef-actions = 3;
	horizon = 20;
	discount = 1.0;
}
